/*
 * Copyright (c) 2013, Francis Galiegue <fgaliegue@gmail.com>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the Lesser GNU General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * Lesser GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
apply(from: "common.gradle");

/*
 * This has to be here... It will not be taken into account in common.gradle!
 *
 * Looks like a serious bug... See also:
 *
 * http://forums.gradle.org/gradle/topics/buildscript_in_subprojects_ignored
 */
buildscript {
    repositories {
        mavenCentral();
    }
    dependencies {
        classpath(group: "eu.appsatori", name: "gradle-fatjar-plugin",
            version: "0.2-rc2");
    }
};
apply(plugin: "fatjar");

/*
 * Repositories to use
 */
repositories {
    mavenCentral();
}

/*
 * Project-specific settings. Unfortunately we cannot put the name in there!
 */
group = "com.github.fge";
version = "1.1.8-SNAPSHOT";
sourceCompatibility = "1.6";
targetCompatibility = "1.6"; // defaults to sourceCompatibility

project.ext {
    description = "Core processing architecture for json-schema-validator";
};

project.ext {
    serviceClasses = [
        "com.github.fge.msgsimple.serviceloader.MessageBundleProvider"
    ];
};

/*
 * List of dependencies
 *
 * Transitive is sort of like Maven"s provided, except it won"t find its way
 * into runtime dependencies
 */
dependencies {
    compile(group: "com.github.fge", name: "jackson-coreutils",
        version: "1.1");
    compile(group: "com.github.fge", name: "uri-template", version: "0.5");
    // Remove when uri-template is updated
    compile(group: "com.github.fge", name: "msg-simple", version: "0.7");
    compile(group: "org.mozilla", name: "rhino", version: "1.7R4");
    compile(group: "com.google.code.findbugs", name: "jsr305", version: "2.0.1") {
        transitive = true
    };
    testCompile(group: "org.testng", name: "testng", version: "6.8") {
        exclude(group: "junit", module: "junit");
        exclude(group: "org.beanshell", module: "bsh");
        exclude(group: "org.yaml", module: "snakeyaml");
    };
    testCompile(group: "org.mockito", name: "mockito-core", version: "1.9.5");
    testCompile(group: "org.easytesting", name: "fest-assert", version: "1.4");
}

javadoc.options.links("http://docs.oracle.com/javase/6/docs/api");
javadoc.options.links("http://jsr-305.googlecode.com/svn/trunk/javadoc");
javadoc.options.links("http://fasterxml.github.com/jackson-databind/javadoc/2.2.0");
javadoc.options.links("http://docs.guava-libraries.googlecode.com/git/javadoc");

/*
 * Necessary! Otherwise TestNG will not be used...
 *
 * Also, we don't want gradle's default HTML report: it does not support
 * parameterized tests which I use a _lot_.
 */
test {
    useTestNG() {
        useDefaultListeners = true;
    };
    testReport = false;
}

/*
 * Necessary to generate the source and javadoc jars
 */
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources";
    from sourceSets.main.allSource;
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc";
    from javadoc.destinationDir;
}

fatJar {
    classifier = "full";
}

processResources {
    dependsOn(generateServiceFiles);
}

artifacts {
    archives jar;
    archives sourcesJar;
    archives javadocJar;
// bad idea:    archives fatJar;
}

signing {
    sign configurations.archives;
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment);
            }

            repository(url: sonatypeRepoURI) {
                authentication(userName: sonatypeUsername,
                    password: sonatypePassword);
            }

            pom.project {
                name "${name}";
                packaging "bundle";
                description "${description}";
                url "${projectURL}";

                scm {
                    url "${gitrwscm}";
                    connection "${gitrwscm}";
                    developerConnection "${gitroscm}";
                }

                licenses {
                    license {
                        name "Lesser General Public License, version 3 or greater";
                        url "http://www.gnu.org/licenses/lgpl.html";
                        distribution "repo";
                    }
                }

                developers {
                    developer {
                        id "fge";
                        name "Francis Galiegue";
                        email "fgaliegue@gmail.com";
                    }
                }
            }
        }
    }
}
